* Implementation

* TODO Implementation
- Conj task tdef
- <<FILE>> PWDâ€¦noweb or custom?

* TODO Babashka support
- Use babashka/fs etc in CLJ (deps.edn).
- #?@(:bb "Babashka!" :clj "CLJ!)
- XDG_CACHE_HOME: (str/join "/" (xdg-cache-home) "et")
- et.<basename>.<task-file>.<task> such as bm-core.chapters.dev.main.build.sh.

#+begin_src clojure :tangle ../src/jakub_stastny/et/utils.clj :mkdirp yes
  (ns jakub-stastny.et.utils)

  (def ^:private colours {:red 31 :green 32 :yellow 33
                          :blue 34 :purple 35 :cyan 36 :grey 37})

  ; https://stackoverflow.com/questions/4842424/list-of-ansi-color-escape-sequences
  (def ^:private font-effects {:normal 0 :bold 1})

  (defn colour
    "Wraps text in ANSI colour escape sequence"
    ([c text]
     (colour c :normal text))

    ([c font-effect text]
     (if (= (System/getenv "TERM") "xterm-256color")
       (let [colour-code (colours c)
             font-effect-code (font-effects font-effect)]
         (str "\033[" font-effect-code ";" colour-code "m" text "\033[0m"))
       text)))

  (defn abort [& chunks]
    (println (apply str (colour :red "Error: ") chunks))
    (System/exit 1))

  (defn puts [& chunks]
    (println (apply str chunks)))

  (defn info [& chunks]
    (apply puts (colour :white :bold "~ ") chunks))

  (defn- task-type [task]
    (cond
      (contains? task :fn) :built-in
      (and (contains? task :path) (contains? task :task-name)) :custom
      true :error))

  (defmulti pr-task "Print task" task-type)

  (defmethod pr-task :built-in [task]
    (let [base-str (str "built-in task " (colour :yellow (name (task :task-name))))]
      (if (empty? (task :args))
        base-str
        (str base-str " and args " (colour :cyan (str (task :args)))))))

  (defmethod pr-task :custom [task]
    (prn :custom task))

  (defmethod pr-task :error [task]
    (prn :error task))
#+end_src

#+begin_src clojure :tangle ../src/jakub_stastny/et/built_ins.clj :mkdirp yes
  (ns jakub-stastny.et.built-ins
    "Built-in tasks."
    (:require [jakub-stastny.et.org :as org]
              [jakub-stastny.et.utils :refer :all]))

  (defn tasks
    "Built-in task to show all tasks in given paths."
    [paths]
    (puts "\n" (colour :blue :bold "Available tasks"))
    (doseq [path paths]
      (let [tasks (filter (fn [task] (get-in task [:opts :doc])) (org/load-tasks path))]
        (when-not (empty? tasks)
          (puts "\nNamespace " (colour :cyan path) ":")
          (doseq [{:keys [name opts]} tasks]
            (puts (colour :yellow name)
                  (apply str (repeat (- 12 (count name)) " "))
                  (opts :doc) "."))))))

  (defn help
    "Built-in task to show help.
     Unlike other tasks can take 0 arguments."
    [& args]
    (puts "\n" (colour :green :bold "Help:"))
    (puts "-T|--tasks task-file.org           # Show documented tasks in given task files")
    (puts "task-file.org task-name arg1 arg2  # Run task-name from task-file.org with arguments arg1 arg2"))

  (def ^:private built-in-tasks
    {:tasks tasks :help help})

  (defn- make-built-in-task
    "Wrap fn as a task." [name]
    {:task-name name :fn (built-in-tasks name)})

  (defn get-task
    "Get a built-in task. Defaults to the help task."
    [arg] (cond
            (some #(= arg %) ["-T" "--tasks" :tasks])
            (make-built-in-task :tasks)

            (some #(= arg %) ["-h" "-H" "--help" :help])
            (make-built-in-task :help)))
#+end_src

#+begin_src clojure :tangle ../src/jakub_stastny/et/config.clj :mkdirp yes
  (ns jakub-stastny.et.config
    (:require [jakub-stastny.et.built-ins :as built-ins]))

  (defn args-to-tasks [args]
    (reduce
     (fn [acc i]
       (cond
         ;; First argument starting with a dash (-T, -h etc).
         (and (empty? acc) (re-matches #"-{1,2}\w+" i))
         (conj acc (built-ins/get-task i))

         (and
          (re-find #"\.org$" i)
          ;; -T is followed by an org file, not a task name.
          ;; (not (= (get-in (last acc) [:task :name]) :tasks))
          (not (= ((last acc) :task-name) :tasks)))
         (conj acc {:path i :args []})

         (and
          (nil? ((last acc) :task-name))
          ;; -T is followed by an org file, not a task name.
          (not (= (get-in (last acc) [:task :name]) :tasks)))
         (conj (butlast acc)
               (assoc-in (last acc) [:task-name] i))

         true
         (conj (butlast acc)
               (update-in (last acc) [:args] #(conj % i)))))
     []
     args))

  (defn task? ; TODO: Use it.
    "..."
    [example]
    (or (example :task) (example :doc)))

  (def default-config
    {:args-to-tasks args-to-tasks
     :task? task?
     :exts {:clojure "clj" :emacs-lisp "el"}
     :cmds {:clojure "clojure -M"}        ; -M -m to run -main OR bb
     :exec-fn clojure.java.shell/sh}) ; Or bb.shell https://github.com/babashka/process
#+end_src

*** TODO spec: validate keys of config (run)
*** TODO Allow overriding built-in tasks in config

#+begin_src clojure :tangle ../src/jakub_stastny/et/runner.clj :mkdirp yes
  (ns jakub-stastny.et.runner
    "Main namespace. Provides the default runner as well as the `run`
    fn for creating custom runners with their own custom config."
    (:gen-class)
    (:require [clojure.string :as str]
              [clojure.java.shell]
              [jakub-stastny.et.utils :refer :all]
              [jakub-stastny.et.config :as config]
              [jakub-stastny.et.built-ins :as built-ins]
              [jakub-stastny.et.org :as org]))

  (defmulti exec-task
    "Docstring"
    (fn [task _] (task-type task)))

  (defmethod exec-task :built-in [task _]
    ((task :fn) (task :args)))

  (defmethod exec-task :custom [task config]
    (org/run-task task config))

  (defmethod exec-task :error [task _]
    ;; (throw (ex-info "Don't know how to handle task" {:task task}))
    ((built-ins/get-task :help) :fn))

  ;; Wrapper
  (defn run-task
    "...."
    [task config]
    (info "Running " (pr-task task))
    (exec-task task config))

  (defn handle-att-error [error]
    (abort "args-to-task: "         ; TODO: make the label customisable.
           (ex-message error) "\n"
           (pr-str (ex-data error))))

  (defn handle-user-error
    "Abort on user error, re-throw otherwise."
    [error]
    (let [info (ex-data error)]
      (if (= (info :type) :user-error)
        (abort (ex-message error))
        (throw error))))

  (defmacro abort-on-error
    [handler body]
    `(try ~body (catch Exception e# (~handler e#))))

  (defn run
    "Main entry fn for a custom runner.
     Takes command-line `custom-config` that it merges
     into the default one and command-line `args`."
    ([args] (run {} args))

    ([custom-config args]
     (abort-on-error
      handle-user-error
      (let [config (conj config/default-config custom-config)]
        (let [defs (abort-on-error handle-att-error ((config :args-to-tasks) args))]
          (doseq [def defs] (run-task def config)))))))

  (defn -main
    "This is the main entry for the default runner.
     Write a custom runner if you want to customise the config."
    [& args] (run args))
#+end_src

#+begin_src clojure :tangle ../src/jakub_stastny/et/org.clj :mkdirp yes
  (ns jakub-stastny.et.org
    "...."
    (:require [clojure.string :as str]
              [clojure.java.io :as io]
              [jakub-stastny.et.utils :refer :all]
              [clojure.java.shell :refer [sh]] ; chmod +x
              [jakub-stastny.et.parser :as parser]))

  (defn load-tasks
    "..."
    [path]
    (if (.exists (io/as-file path)) ; Replace by fs/exists from bb.
      (parser/parse-examples (str/split (slurp path) #"\n"))
      (throw (ex-info (str "File " path "doesn't exist") {:type :user-error}))))

  (defn write-script-2
    "...."
    [script-name command lines task-file-path]
    (let [shebang (str "#!/usr/bin/env " command)
          indent (re-find #"^ *" (first lines))]
      (spit script-name
            (str/join "\n"
                      (apply conj
                             [shebang ""]
                             (map
                              (fn [line] (-> line
                                             (str/replace indent "")
                                             (str/replace #"\{\{\s*[CP]WD\s*\}\}" (System/getenv "PWD"))
                                             (str/replace #"\{\{\s*FILE\s*\}\}" task-file-path)))
                              (conj lines "")))))
      (sh "chmod" "+x" script-name)))

  (defn get-ext
    "..."
    [exts lang] (or (get exts (keyword lang)) lang))

  (defn get-cmd
    "..."
    [cmds lang] (or (get cmds (keyword lang)) lang))

  ; TODO: don't rewrite if same.
  (defn write-script
    "....."
    [{:keys [name lang lines opts]} {:keys [exts cmds]}]
    (let [shebang (if (opts :shebang)
                    (str/split (str/replace (opts :shebang) #"#!" "") #"\s+")
                    (str/split (get-cmd cmds lang) #"\s+"))
          script-path (str "/tmp/" name "." (get-ext exts lang))]
      (write-script-2 script-path (str/join " " shebang) lines "ReplaceMe.org")

      (conj shebang script-path)))

  (defn exec-task
    "..."
    [task task-def config]
    (info "Running task " (task :name) " " (pr-str (task :opts)))
    (info "Running task " (pr-task task))
    (let [exec-fn (config :exec-fn)
          base-command (write-script task config)
          command (apply conj base-command (task-def :args))]
      ;; With exec it'll never run so no prob.
      ;; (prn command) (prn res) (puts)
      (info "Running " (colour :green "$ ") (colour :blue (str/join " " command)) "\n")
      (let [res (apply exec-fn command)]
        (if (= (res :exit) 0)
          (puts (str/trim (res :out)))
          (puts (str/trim (res :err))))
        (System/exit (res :exit)))))

  (defn run-task
    "..."
    [task-def config]
    (let [examples (load-tasks (task-def :path))
          tasks (filter #(= ((% :opts) :task) (symbol "yes")) examples)
          task (first (filter #(= (task-def :task-name) (% :name)) tasks))]
      (if task
        (exec-task task task-def config)
        (puts "No such task " (task-def :task-name) "\n\nAvailable tasks: " (pr-str tasks)))))
  ;; (abort (str "No such task: " task-name "\nTasks in this namespace: ...."))

  ;; (defn filter-tasks [namespace]
  ;;     (filter (fn [task]
  ;;             (= (symbol "yes") ((task :opts) :task)))
  ;;     (parse-examples (get-lines-or-abort namespace))))


  ;;    (defn run [fully-qualified-task-name]
  ;;      (let [namespace (first (str/split fully-qualified-task-name #"/"))
  ;;            task-name (last (str/split fully-qualified-task-name #"/"))
  ;;            lines (get-lines-or-abort namespace)
  ;;            tasks (filter-tasks namespace)]
  ;;        (exec tasks task-name)))
  ;;            (run a)))
  ;; (defn convert-ns-to-path [namespace]
  ;;   (when (or (re-find #"[/:]" namespace))
  ;;     (throw (Exception. "Namespace uses . rather than /")))
  ;;   (str (str/replace namespace #"\." "/") ".org"))

  ;; (prn (babel-to-map ""))
  ;; (prn (babel-to-map ":tangle test.clj"))
  ;; (prn (babel-to-map ":tangle \"test.clj\""))
  ;; (prn (babel-to-map ":shebang \"#!/usr/bin/env clojure -M\""))
  ;; (prn (babel-to-map ":task yes :shebang \"#!/usr/bin/env clojure -M\""))
  ;; (puts)
  ;; (System/exit 1)
#+end_src

#+begin_src clojure :tangle ../src/jakub_stastny/et/parser.clj :mkdirp yes
  (ns jakub-stastny.et.parser
    "...."
    (:require [clojure.string :as str]
              [clojure.edn :as edn]))

  (defn parse-var [line]
  (str/trim (str/replace line #"^\s*#\+\w+:?(.*)$" "$1")))

  (defn babel-to-map [string]
      (edn/read-string (str "{" string "}")))

  (defn parse-examples [lines]
    (let [update-last-task
          (fn [tasks update-fn]
            (conj
             (vec (butlast tasks))
             (conj (last tasks) (update-fn (last tasks)))))]

      (first
       (reduce
        (fn [[tasks status] line]
          ;; (prn {:t tasks :s status :l line}) ; --------------
          (cond
            ;; Read name.
            (re-find #"^\s*#\+(?i)name:" line)
            [(conj tasks {:name (parse-var line)}) :named]

            ;; Read block options.
            (and (re-find #"^\s*#\+(?i)begin_src" line)
                 (= status :named))
            [(update-last-task
              tasks
              (fn [task]
                (let [raw-opts (str/split (parse-var line) #"\s+")
                      lang (first raw-opts)
                      opts (babel-to-map (str/join " " (rest raw-opts)))]
                  {:lines [] :lang lang :opts opts})))
             :reading]

            ;; Stop reading block body.
            ;; We do need this line even though it does the same
            ;; as the default cond so the end_src line gets skipped.
            (re-find #"^\s*#\+(?i)end_src" line)
            [tasks nil]

            ;; Read body.
            (= status :reading)
            [(update-last-task
              tasks
              (fn [task] {:lines (vec (conj (:lines task) line))}))
             :reading]

            :default [tasks nil]))
        [[] nil]
        lines))))
#+end_src
